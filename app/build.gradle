apply plugin: 'com.android.application'

Properties props = new Properties()
File propsFile = file('tmdb-credential.properties')
if (propsFile.exists()) {
    props.load(propsFile.newDataInputStream())
} else {
    throw new GradleException("Missing tmdb-credential.properties, check the tmdb-credential.properties.properties.sample file.")
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.psimao.themovieapp"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField "String", "TMDB_KEY", "\"${props.getProperty("tmdb_key")}\""
        }
        release {
            buildConfigField "String", "TMDB_KEY", "\"${props.getProperty("tmdb_key")}\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // standard jars
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // support libraries
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportVersion"
    compile "com.android.support:design:$rootProject.ext.supportVersion"
    compile "com.android.support:cardview-v7:$rootProject.ext.supportVersion"
    // constraint layout
    compile "com.android.support.constraint:constraint-layout:$rootProject.ext.constraintVersion"
    // picasso
    compile "com.squareup.picasso:picasso:$rootProject.ext.picassoVersion"
    // butter knife
    compile "com.jakewharton:butterknife:$rootProject.ext.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterKnifeVersion"
    // dagger 2
    compile "com.google.dagger:dagger:$rootProject.ext.daggerVersion"
    compile "com.google.dagger:dagger-android:$rootProject.ext.daggerVersion"
    compile "com.google.dagger:dagger-android-support:$rootProject.ext.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$rootProject.ext.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"
    // rxjava 2
    compile "io.reactivex.rxjava2:rxandroid:$rootProject.ext.rxAndroidVersion"
    compile "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxJavaVersion"
    // retrofit
    compile "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava2:$rootProject.ext.retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofitVersion"
    // jUnit
    testCompile "junit:junit:$rootProject.ext.jUnitVersion"
    // espresso
    androidTestCompile("com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion", {
        exclude group: "com.android.support", module: "support-annotations"
        exclude group: "com.google.code.findbugs"
    })
}
